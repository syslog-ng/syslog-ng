/*
 * Copyright (c) 2002-2011 Balabit
 * Copyright (c) 1998-2011 Bal√°zs Scheidler
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */

%code requires {

#include "pragma-parser.h"
#include "plugin.h"
#include "cfg.h"
#include "messages.h"

#include <string.h>

}

%name-prefix "pragma_"
%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {gpointer *result}
%parse-param {gpointer arg}

/* INCLUDE_DECLS */

%token KW_VERSION
%token KW_DEFINE
%token KW_MODULE

%code {

#include <stdlib.h>

CfgArgs *last_module_args = NULL;

gint
__process_version_string(gchar *value)
{
  gchar *p, *end;
  gint major, minor;

  p = strchr(value, '.');
  if (p)
    {
      major = strtol(value, &end, 10);
      if (end == p)
        {
          minor = strtol(p+1, &end, 10);
          if (end)
            {
              return (major << 8) + minor;
            }
        }
    }
  return 0;
}

}

%%

start
        : { lexer->tokenize_eol++; }
          stmt_and_eol                                  { lexer->tokenize_eol--; if (yychar != YYEMPTY) { cfg_lexer_unput_token(lexer, &yylval); } YYACCEPT; }

stmt_and_eol
        : pragma_stmt LL_EOL
        | include_stmt
        ;

pragma_stmt
        : KW_VERSION ':' string_or_number               { configuration->parsed_version = __process_version_string($3); free($3); }
	| define_stmt
        | module_stmt
	;

include_stmt
        : KW_INCLUDE string LL_EOL
          {
            CHECK_ERROR(cfg_lexer_include_file(lexer, $2), @2, "Error including %s", $2);
            free($2);
          }
        ;

define_stmt
	: KW_DEFINE LL_IDENTIFIER string_or_number		{ msg_debug("Global value changed",
                                                                            evt_tag_str("define", $2),
                                                                            evt_tag_str("value", $3));
								  cfg_args_set(configuration->globals, $2, $3); free($2); free($3); }

module_stmt
	: KW_MODULE string { last_module_args = cfg_args_new(); } module_params
          {
            plugin_load_module(&configuration->plugin_context, $2, last_module_args);
            free($2);
            cfg_args_unref(last_module_args);
            last_module_args = NULL;
          }
	;

module_params
	: module_param module_params
	|
	;

module_param
	: LL_IDENTIFIER '(' string_or_number ')'		{ cfg_args_set(last_module_args, $1, $3); free($1); free($3); }
	;

/* INCLUDE_RULES */

%%
