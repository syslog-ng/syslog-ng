/*
 * Copyright (c) 2023 Balazs Scheidler <balazs.scheidler@axoflow.com>
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */

%code requires {

#include "filterx/filterx-parser.h"

}

%code {

/* filterx language constructs */
#include "filterx/expr-literal.h"
#include "filterx/expr-message-ref.h"
#include "filterx/expr-template.h"
#include "filterx/expr-boolalg.h"
#include "filterx/expr-assign.h"
#include "filterx/expr-getattr.h"
#include "filterx/expr-setattr.h"
#include "filterx/expr-json.h"
#include "filterx/object-primitive.h"
#include "filterx/object-json.h"
#include "filterx/object-null.h"

#include "template/templates.h"
}

%define api.prefix {filterx_}
%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {GList **result}
%parse-param {gpointer arg}

/* INCLUDE_DECLS */

%token KW_TRUE, KW_FALSE
%token KW_NULL

%type <ptr> stmts
%type <node> stmt
%type <node> expr
%type <node> expr_value
%type <node> literal
%type <node> lvalue
%type <ptr> template
%type <ptr> json_object
%type <ptr> json_object_values
%type <ptr> json_object_value
%type <num> boolean

%%

start
        : stmts					{ *result = $1; if (yychar != FILTERX_EMPTY) { cfg_lexer_unput_token(lexer, &yylval); } YYACCEPT; }
	;

stmts
	: stmt stmts				{ $$ = g_list_prepend($2, $1); }
	|					{ $$ = NULL; }
	;

stmt
	: expr ';'				{ $$ = $1; }

expr
	: expr_value				{ $$ = $1; }
	| lvalue KW_ASSIGN expr			{ $$ = filterx_assign_new($1, $3); }
	| KW_NOT expr				{ $$ = filterx_unary_not_new($2); }
	| expr KW_OR expr			{ $$ = filterx_binary_or_new($1, $3); }
	| expr KW_AND expr                      { $$ = filterx_binary_and_new($1, $3); }
	| expr '.' LL_IDENTIFIER		{ $$ = filterx_getattr_new($1, $3); free($3); }
	| expr '.' LL_IDENTIFIER KW_ASSIGN expr	{ $$ = filterx_setattr_new($1, $3, $5); free($3); }
	| '(' expr ')'				{ $$ = $2; }
	;

expr_value
	: literal
	| lvalue				{ $$ = $1; }
	| json_object                           { $$ = filterx_json_expr_new($1); }
	;

literal
	: LL_NUMBER				{ $$ = filterx_literal_new(filterx_integer_new($1)); }
	| LL_FLOAT				{ $$ = filterx_literal_new(filterx_double_new($1)); }
	| KW_NULL				{ $$ = filterx_literal_new(filterx_null_new()); }
	| template				{ $$ = filterx_template_new($1); }
	| boolean				{ $$ = filterx_literal_new(filterx_boolean_new($1)); }
	;

lvalue: '$' LL_IDENTIFIER			{ $$ = filterx_message_ref_expr_new(log_msg_get_value_handle($2)); free($2); }


boolean
	: KW_TRUE				{ $$ = 1; }
	| KW_FALSE				{ $$ = 0; }
	;

template
	: LL_STRING
          {
            GError *error = NULL;
	    LogTemplate *template = log_template_new(configuration, NULL);

            CHECK_ERROR_GERROR(log_template_compile(template, $1, &error), @1, error, "Error compiling template");
            free($1);

            $$ = template;
          }
	;

json_object
	: '{' json_object_values '}'            { $$ = $2; }
	;

json_object_values
	: json_object_value ',' json_object_values      { $$ = g_list_prepend($3, $1); }
	|						{ $$ = NULL; }
	;

json_object_value
	: LL_STRING ':' expr				{ $$ = filterx_json_kv_new($1, $3); free($1); }
	;

/* INCLUDE_RULES */

%%
