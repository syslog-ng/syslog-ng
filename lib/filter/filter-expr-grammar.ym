/*
 * Copyright (c) 2002-2011 BalaBit IT Ltd, Budapest, Hungary
 * Copyright (c) 1998-2011 Bal√°zs Scheidler
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */

%code requires {

#include "filter/filter-expr-parser.h"

}

%code {

#include "filter/filter-expr-grammar.h"
#include "filter/filter-netmask.h"
#include "filter/filter-op.h"
#include "filter/filter-cmp.h"
#include "filter/filter-tags.h"
#include "filter/filter-call.h"
#include "filter/filter-re.h"
#include "filter/filter-pri.h"
#include "syslog-names.h"
#include "plugin.h"

FilterRE *last_re_filter;

}

%name-prefix "filter_expr_"
%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {FilterExprNode **result}
%parse-param {gpointer arg}

/* INCLUDE_DECLS */

%token KW_PROGRAM

%left   ';'
%left	KW_OR
%left	KW_AND
%left   KW_NOT
%left   KW_LT KW_LE KW_EQ KW_NE KW_GE KW_GT
%left   KW_NUM_LT KW_NUM_LE KW_NUM_EQ KW_NUM_NE KW_NUM_GE KW_NUM_GT

%type	<node> filter_expr
%type	<node> filter_simple_expr

%type   <num> filter_fac_list
%type	<num> filter_level_list
%type	<num> filter_level

%type   <token> operator

%%

start
        : filter_expr                           { *result = $1; if (yychar != YYEMPTY) { cfg_lexer_unput_token(lexer, &yylval); } YYACCEPT; }
	;

filter_expr
	: filter_simple_expr			{ $$ = $1; if (!$1) YYERROR; }
        | KW_NOT filter_expr			{ ((FilterExprNode *) $2)->comp = !(((FilterExprNode *) $2)->comp); $$ = $2; }
	| filter_expr KW_OR filter_expr		{ $$ = fop_or_new($1, $3); }
	| filter_expr KW_AND filter_expr	{ $$ = fop_and_new($1, $3); }
	| filter_expr ';' filter_expr	        { $$ = fop_and_new($1, $3); }
	|  filter_expr ';'	                { $$ = $1; }
	| '(' filter_expr ')'			{ $$ = $2; }
	;

filter_simple_expr
	: KW_FACILITY '(' filter_fac_list ')'	{ $$ = filter_facility_new($3);  }
	| KW_FACILITY '(' LL_NUMBER ')'		{ $$ = filter_facility_new(0x80000000 | $3); }
	| KW_LEVEL '(' filter_level_list ')' 	{ $$ = filter_level_new($3); }
	| KW_FILTER '(' string ')'		{ $$ = filter_call_new($3, configuration); free($3); }
	| KW_NETMASK '(' string ')'		{ $$ = filter_netmask_new($3); free($3); }
        | KW_TAGS '(' string_list ')'           { $$ = filter_tags_new($3); }
	| KW_PROGRAM '(' string
	  {
	    last_re_filter = (FilterRE *) filter_re_new(LM_V_PROGRAM);
          }
          filter_re_opts ')'
          {
            if(!filter_re_set_regexp(last_re_filter, $3))
              YYERROR;
            free($3);

            $$ = &last_re_filter->super;
          }
	| KW_HOST '(' string
	  {
	    last_re_filter = (FilterRE *) filter_re_new(LM_V_HOST);
          }
          filter_re_opts ')'
          {
            if(!filter_re_set_regexp(last_re_filter, $3))
              YYERROR;
            free($3);

            $$ = &last_re_filter->super;
          }
	| KW_MATCH '(' string
	  {
	    last_re_filter = (FilterRE *) filter_match_new();
	  }
          filter_match_opts ')'
          {
            if(!filter_re_set_regexp(last_re_filter, $3))
              YYERROR;
            free($3);
            $$ = &last_re_filter->super;

            if (last_re_filter->value_handle == 0)
              {
                static gboolean warn_written = FALSE;

                if (!warn_written)
                  {
                    msg_warning("WARNING: the match() filter without the use of the value() option is deprecated and hinders performance, please update your configuration",
                                NULL);
                    warn_written = TRUE;
                  }
              }
          }
        | KW_MESSAGE '(' string
          {
	    last_re_filter = (FilterRE *) filter_re_new(LM_V_MESSAGE);
          }
          filter_re_opts ')'
          {
            if(!filter_re_set_regexp(last_re_filter, $3))
              YYERROR;
	    free($3);
            $$ = &last_re_filter->super;
          }
        | KW_SOURCE '(' string
          {
	    last_re_filter = (FilterRE *) filter_re_new(LM_V_SOURCE);
            filter_re_set_matcher(last_re_filter, log_matcher_string_new());
          }
          filter_re_opts ')'
          {
            if(!filter_re_set_regexp(last_re_filter, $3))
              YYERROR;
	    free($3);
            $$ = &last_re_filter->super;
          }
        | LL_IDENTIFIER
          {
            Plugin *p;
            gint context = LL_CONTEXT_FILTER;
            FilterExprNode *node;

            p = plugin_find(configuration, context, $1);
            CHECK_ERROR(p, @1, "%s plugin %s not found", cfg_lexer_lookup_context_name_by_type(context), $1);

            node = (FilterExprNode *) plugin_parse_config(p, configuration, &@1, NULL);
            free($1);
            if (!node)
              {
                YYERROR;
              }
            $$ = node;
          }
	| string operator string
          {
            LogTemplate *left, *right;
            GError *error = NULL;
            gboolean success_left, success_right;

            left = log_template_new(configuration, NULL);
            right = log_template_new(configuration, NULL);
            success_left = log_template_compile(left, $1, &error);
            success_right = log_template_compile(right, $3, &error);
            free($1);
            free($3);
            CHECK_ERROR(success_left, @1, "error compiling template: %s", error->message);
            CHECK_ERROR(success_right, @3, "error compiling template: %s", error->message);

            $$ = fop_cmp_new(left, right, $2);
          }
        ;

operator
	: KW_NUM_LT	{ $$ = yylval.token; }
	| KW_NUM_LE	{ $$ = yylval.token; }
	| KW_NUM_EQ     { $$ = yylval.token; }
	| KW_NUM_NE     { $$ = yylval.token; }
	| KW_NUM_GE     { $$ = yylval.token; }
	| KW_NUM_GT     { $$ = yylval.token; }
	| KW_LT		{ $$ = yylval.token; }
	| KW_LE		{ $$ = yylval.token; }
	| KW_EQ         { $$ = yylval.token; }
	| KW_NE         { $$ = yylval.token; }
	| KW_GE         { $$ = yylval.token; }
	| KW_GT         { $$ = yylval.token; }
	;

filter_match_opts
        : filter_match_opt filter_match_opts
        |
        ;

filter_match_opt
        : filter_re_opt
        | KW_VALUE '(' string ')'
          {
            const gchar *p = $3;
            if (p[0] == '$')
              {
                msg_warning("Value references in filters should not use the '$' prefix, those are only needed in templates",
                            evt_tag_str("value", $3),
                            NULL);
                p++;
              }
            last_re_filter->value_handle = log_msg_get_value_handle(p);
            free($3);
          }
        ;

filter_re_opts
        : filter_re_opt filter_re_opts
        |
        ;

filter_re_opt
        : KW_TYPE '(' string ')'
          {
            filter_re_set_matcher(last_re_filter, log_matcher_new($3));
            free($3);
          }
        | KW_FLAGS '(' regexp_option_flags ')' { filter_re_set_flags(last_re_filter, $3); }
        ;


filter_fac_list
	: facility_string filter_fac_list	{ $$ = (1 << ($1 >> 3)) | $2; }
	| facility_string			{ $$ = (1 << ($1 >> 3)); }
	;

filter_level_list
	: filter_level filter_level_list	{ $$ = $1 | $2; }
	| filter_level				{ $$ = $1; }
	;

filter_level
	: level_string LL_DOTDOT level_string
	  {
	    $$ = syslog_make_range($1, $3);
	  }
	| level_string
	  {
	    $$ = 1 << $1;
	  }
	;


/* INCLUDE_RULES */

%%
