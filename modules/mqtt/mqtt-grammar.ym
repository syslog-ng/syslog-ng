/*
 * Copyright (c) 2021 One Identity
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms of the GNU General Public License version 2 as published
 * by the Free Software Foundation, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */


%code requires {

#include "mqtt-parser.h"

}

%code {

#include "mqtt-destination.h"

#include "cfg-grammar-internal.h"
#include "plugin.h"
}

%define api.prefix {mqtt_destination_}
%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {LogDriver **instance}
%parse-param {gpointer arg}

/* INCLUDE_DECLS */

%token KW_MQTT
%token KW_TOPIC
%token KW_FALLBACK_TOPIC
%token KW_KEEPALIVE
%token KW_ADDRESS
%token KW_QOS
%token KW_MESSAGE_TEMPLATE
%token KW_USERNAME
%token KW_PASSWORD
%token KW_HTTP_PROXY
%token KW_TLS
%token KW_CA_DIR
%token KW_CA_FILE
%token KW_CERT_FILE
%token KW_KEY_FILE
%token KW_CIPHER_SUITE
%token KW_USE_SYSTEM_CERT_STORE
%token KW_SSL_VERSION
%token KW_PEER_VERIFY

%%

start
        : LL_CONTEXT_DESTINATION KW_MQTT
          {
            last_driver = *instance = mqtt_dd_new(configuration);
          }
          '(' mqtt_destination_options ')' { YYACCEPT; }
;

mqtt_destination_options
        : mqtt_destination_option mqtt_destination_options
        |
        ;

mqtt_destination_option
        : KW_TOPIC '(' template_content ')'     { mqtt_dd_set_topic_template(last_driver, $3);  }
        | KW_FALLBACK_TOPIC '(' string ')'      { mqtt_dd_set_fallback_topic(last_driver, $3); free($3); }
        | KW_KEEPALIVE '(' positive_integer ')' { mqtt_dd_set_keepalive(last_driver, $3);       }
        | KW_ADDRESS '(' string ')'
          {
            CHECK_ERROR(mqtt_dd_validate_address($3), @1, "Invalid address");
            mqtt_dd_set_address(last_driver, $3);
            free($3);
          }
        | KW_QOS '(' nonnegative_integer ')'
          {
            CHECK_ERROR(0 <= $3 && $3 <= 2, @1, "Allowed QOS values: 0, 1, 2");
            mqtt_dd_set_qos(last_driver, $3);
          }
        | KW_TEMPLATE '(' template_content ')'   { mqtt_dd_set_message_template_ref(last_driver, $3); }
        | KW_USERNAME '(' string ')'             { mqtt_dd_set_username(last_driver, $3); free($3); }
        | KW_PASSWORD '(' string ')'             { mqtt_dd_set_password(last_driver, $3); free($3); }
        | KW_HTTP_PROXY '(' string ')'           { mqtt_dd_set_http_proxy(last_driver, $3); free($3); }
        | KW_TLS '(' mqtt_tls_options ')'
        | threaded_dest_driver_general_option
        | { last_template_options = mqtt_dd_get_template_options(last_driver); } template_option
        ;

mqtt_tls_options
    : mqtt_tls_option mqtt_tls_options
    |
    ;

mqtt_tls_option
    : KW_CA_DIR     '(' string ')'            { mqtt_dd_set_ca_dir(last_driver, $3); free($3); }
    | KW_CA_FILE    '(' path_check ')'        { mqtt_dd_set_ca_file(last_driver, $3); free($3); }
    | KW_CERT_FILE  '(' path_check ')'        { mqtt_dd_set_cert_file(last_driver, $3); free($3); }
    | KW_KEY_FILE   '(' path_secret ')'       { mqtt_dd_set_key_file(last_driver, $3); free($3); }
    | KW_CIPHER_SUITE '(' string ')'          { mqtt_dd_set_cipher_suite(last_driver, $3); free($3); }
    | KW_SSL_VERSION '(' string ')'           { CHECK_ERROR(mqtt_dd_set_ssl_version(last_driver, $3), @3,
                                                            "mqtt: unsupported SSL version: %s", $3);
                                                free($3); }
    | KW_PEER_VERIFY '(' yesno ')'            { mqtt_dd_set_peer_verify(last_driver, $3); }
    | KW_USE_SYSTEM_CERT_STORE '(' yesno ')'  { mqtt_dd_use_system_cert_store(last_driver, $3); }
    ;

/* INCLUDE_RULES */

%%
