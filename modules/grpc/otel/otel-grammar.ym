/*
 * Copyright (c) 2024 Axoflow
 * Copyright (c) 2023-2024 Attila Szakacs <attila.szakacs@axoflow.com>
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms of the GNU General Public License version 2 as published
 * by the Free Software Foundation, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */

/* REQUIRE modules/grpc/common/grpc-grammar.ym */

%code top {
#include "otel-parser.h"

}

%code {

#include "cfg-grammar-internal.h"
#include "cfg-helpers.h"
#include "plugin.h"
#include "syslog-names.h"
#include "otel-source.h"
#include "otel-protobuf-parser.h"
#include "otel-dest.h"
#include "syslog-ng-otlp-dest.h"
}

%define api.prefix {otel_}

/* this parameter is needed in order to instruct bison to use a complete
 * argument list for yylex/yyerror */

%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {void **instance}
%parse-param {gpointer arg}

/* INCLUDE_DECLS */

%token KW_OPENTELEMETRY
%token KW_SYSLOG_NG_OTLP
%token KW_SET_HOSTNAME

%type <ptr> source_otel
%type <ptr> parser_otel
%type <ptr> destination_otel
%type <ptr> destination_syslog_ng_otlp

%%

start
  : LL_CONTEXT_SOURCE source_otel { YYACCEPT; }
  | LL_CONTEXT_PARSER parser_otel { YYACCEPT; }
  | LL_CONTEXT_DESTINATION destination_otel { YYACCEPT; }
  | LL_CONTEXT_DESTINATION destination_syslog_ng_otlp { YYACCEPT; }
  ;

kw_opentelemetry_or_syslog_ng_otlp
  : KW_OPENTELEMETRY
  | KW_SYSLOG_NG_OTLP
  ;

source_otel
  : kw_opentelemetry_or_syslog_ng_otlp
    {
      last_driver = *instance = otel_sd_new(configuration);
    }
    '(' _inner_src_context_push source_otel_options _inner_src_context_pop ')' { $$ = last_driver; }
  ;

source_otel_options
  : source_otel_option source_otel_options
  |
  ;

source_otel_option
  : grpc_source_option
  ;

parser_otel
  : KW_OPENTELEMETRY
    {
      last_parser = *instance = otel_protobuf_parser_new(configuration);
    }
    '(' parser_otel_options ')' { $$ = last_parser; }
  ;

parser_otel_options
  : parser_otel_option parser_otel_options
  |
  ;

parser_otel_option
  : parser_opt
  | KW_SET_HOSTNAME '(' yesno ')' { otel_protobuf_parser_set_hostname(last_parser, $3); }
  ;

destination_otel
  : KW_OPENTELEMETRY
    {
      last_driver = *instance = otel_dd_new(configuration);
    }
    '(' _inner_dest_context_push destination_otel_options _inner_dest_context_pop ')' { $$ = last_driver; }
  ;

destination_otel_options
  : destination_otel_option destination_otel_options
  |
  ;

destination_otel_option
  : grpc_dest_general_option
  ;

destination_syslog_ng_otlp
  : KW_SYSLOG_NG_OTLP
    {
      last_driver = *instance = syslog_ng_otlp_dd_new(configuration);
    }
    '(' _inner_dest_context_push destination_syslog_ng_otlp_options _inner_dest_context_pop ')' { $$ = last_driver; }
  ;

destination_syslog_ng_otlp_options
  : destination_syslog_ng_otlp_option destination_syslog_ng_otlp_options
  |
  ;

destination_syslog_ng_otlp_option
  : destination_otel_option
  ;

/* INCLUDE_RULES */

%%
