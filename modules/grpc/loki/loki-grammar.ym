/*
 * Copyright (c) 2023 László Várady
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms of the GNU General Public License version 2 as published
 * by the Free Software Foundation, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */

/* REQUIRE modules/grpc/common/grpc-grammar.ym */

%code top {
#include "loki-parser.h"

}


%code {

#include "cfg-grammar-internal.h"
#include "plugin.h"
#include "syslog-names.h"

#include "loki-dest.h"

#include <stdlib.h>

}

%define api.prefix {loki_}

%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {LogDriver **instance}
%parse-param {gpointer arg}

/* INCLUDE_DECLS */

%token KW_LOKI
%token KW_LABELS
%token KW_TIMESTAMP
%token KW_TENANT_ID

%type <ptr> loki_dest

%%

start
  : LL_CONTEXT_DESTINATION loki_dest { YYACCEPT; }
  ;

loki_dest
  : KW_LOKI
    {
      last_driver = *instance = loki_dd_new(configuration);
    }
    '(' _inner_dest_context_push loki_dest_options _inner_dest_context_pop ')' { $$ = last_driver; }
  ;

loki_dest_options
  : loki_dest_option loki_dest_options
  |
  ;

loki_dest_option
  : KW_LABELS '(' loki_labels ')'
  | KW_TENANT_ID '(' string ')' { loki_dd_set_tenant_id(last_driver, $3); free($3); }
  | KW_TIMESTAMP '(' string ')'
    {
      CHECK_ERROR(loki_dd_set_timestamp(last_driver, $3), @1, "Failed to set timestamp(). Valid values are: \"current\", \"received\", \"msg\"");
      free($3);
    }
  | KW_TEMPLATE '(' template_name_or_content ')' { loki_dd_set_message_template_ref(last_driver, $3); }
  | grpc_dest_option
  | { last_template_options = loki_dd_get_template_options(last_driver); } template_option
  ;

loki_labels
  : loki_label loki_labels
  |
  ;

loki_label
  : string LL_ARROW template_content
    {
	    loki_dd_add_label(last_driver, $1, $3);
      free($1);
      log_template_unref($3);
    }
  ;

/* INCLUDE_RULES */

%%
