/*
 * Copyright (c) 2014 Balabit
 * Copyright (c) 2014 Viktor Juhasz <viktor.juhasz@balabit.com>
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */

%code requires {

#include "java-parser.h"

}

%code {

#include "cfg-parser.h"
#include "cfg-grammar.h"
#include "java-config.h"
#include "plugin.h"

}

%name-prefix "java_"
%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {LogDriver **instance}
%parse-param {gpointer arg}


/* INCLUDE_DECLS */

%token KW_JAVA
%token KW_CLASS_PATH
%token KW_CLASS_NAME
%token KW_OPTION
%token KW_JVM_OPTIONS

%%

start
        : LL_CONTEXT_DESTINATION KW_JAVA
          {
            last_driver = *instance = java_dd_new(configuration);
          }
          '(' java_dest_options ')'		{ YYACCEPT; }
        | LL_CONTEXT_OPTIONS java_global_option { YYACCEPT; }
        ;

java_dest_options
        : java_dest_option java_dest_options
        |
        ;

java_dest_option
        : KW_CLASS_PATH '(' string ')'
          {
          	java_dd_set_class_path(last_driver, $3);
            free($3);
          }
        | KW_CLASS_NAME '(' string ')'
          {
          	java_dd_set_class_name(last_driver, $3);
            free($3);
          }
        | KW_TEMPLATE '(' string ')' { java_dd_set_template_string(last_driver, $3); free($3); }
        | KW_OPTION '(' java_dest_custom_options ')'
        | threaded_dest_driver_option
        | { last_template_options = java_dd_get_template_options(last_driver); } template_option
        ;

java_dest_custom_options
  : java_dest_custom_option java_dest_custom_options
  |
  ;

java_dest_custom_option
  : string string_or_number { java_dd_set_option(last_driver, $1, $2); free($1); free($2); }

java_global_option
        : KW_JVM_OPTIONS '(' string ')' {java_config_set_jvm_options(configuration, g_strdup($3)); free($3);}
	;
/* INCLUDE_RULES */

%%
