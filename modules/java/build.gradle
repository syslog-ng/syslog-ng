apply plugin: 'java'
apply plugin: 'eclipse'

project.buildDir = syslogBuildDir+'/syslog-ng-core'
archivesBaseName = 'syslog-ng-core'

task nativeHeaders {
    def classes = [
            'org.syslog_ng.LogTemplate',
            'org.syslog_ng.LogMessage',
            'org.syslog_ng.LogDestination',
            'org.syslog_ng.InternalMessageSender'
    ]

    def outputFiles = [
            file("$syslogBuildDir/org_syslog_ng_LogTemplate.h"),
            file("$syslogBuildDir/org_syslog_ng_LogMessage.h"),
            file("$syslogBuildDir/org_syslog_ng_LogDestination.h"),
            file("$syslogBuildDir/org_syslog_ng_InternalMessageSender.h")
    ]

    inputs.files sourceSets.main.output
    inputs.property('classes', classes)
    outputs.files outputFiles
    doLast {
        exec {
            executable org.gradle.internal.jvm.Jvm.current().getExecutable('javah')
            args '-classpath', sourceSets.main.output.classesDir
            args '-d', syslogBuildDir
            args classes
        }
    }
}

build.dependsOn nativeHeaders

repositories {
    maven {
       url "http://build.syslog-ng.balabit:8080/repository/syslog-ng-pe/"
       credentials {
                        username "syslog-ng"
                        password "titkos12"
       }
    }
}
